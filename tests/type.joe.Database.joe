// type.joe.Database.joe
//
// This script tests the behavior of Database objects.

//-----------------------------------------------------------------------------
// Helper types

record Parent(parent, child) {}
record Ancestor(ancestor, descendant) {}
record Person(name, age) {}
record Place(id) {}
record Thing(id, color) {}

var JOE = Person(#joe, 90);
var JOE_FACT = Joe.toFact(JOE);
var TEXAS = Place(#texas);
var TEXAS_FACT = Joe.toFact(TEXAS);
var HAT = Thing(#hat, #black);
var HAT_FACT = Joe.toFact(HAT);

//-----------------------------------------------------------------------------
// Creation

function testCreation_empty() {
    var db = Database();
    assertEmpty(db);
    assertEQ(db.size(), 0);
    assertT(db.all().isEmpty());
    assertT(db.relations().isEmpty());
    assertT(db.relation("Thing").isEmpty());
    assertEQ(db.toString(), "Database[0]");
}

function testCreation_collection() {
    var db = Database([JOE, TEXAS, HAT]);
    assertF(db.isEmpty());
    assertEQ(db.size(), 3);
    assertF(db.all().isEmpty());
    assertEQ(db.all(), {JOE_FACT, TEXAS_FACT, HAT_FACT});
    assertF(db.relations().isEmpty());
    assertEQ(db.relations(), {"Person", "Place", "Thing"});
    assertEQ(db.relation("Person"), {JOE_FACT});
    assertEQ(db.toString(), "Database[3, Person[1], Place[1], Thing[1]]");
}

function testCreation_Database() {
    var other = Database([JOE, HAT]);

    var db = Database(other);
    assertF(db.isEmpty());
    assertEQ(db.size(), 2);
    assertF(db.all().isEmpty());
    assertEQ(db.all(), other.all());
    assertF(db.relations().isEmpty());
    assertEQ(db.relations(), other.relations());
    assertEQ(db.relation("Person"), other.relation("Person"));
    assertEQ(db.toString(), "Database[2, Person[1], Thing[1]]");
}

function testCreation_badInputs() {
    var inputs = ["abc"];

    assertError(\-> Database(inputs),
        "Expected Nero-compatible fact, got: String 'abc'.");
}

//-------------------------------------------------------------------------
// addFacts

function testAddFacts_collection() {
    var db = Database();
    db.addFacts([JOE, HAT]);
    assertEQ(db.all(), {JOE_FACT, HAT_FACT});
    assertEQ(db.relation("Person"), {JOE_FACT});
}

function testAddFacts_Database() {
    var db = Database();
    var other = Database([JOE, HAT]);

    db.addFacts(other);
    assertEQ(db.all(), other.all());
    assertEQ(db.relation("Person"), other.relation("Person"));
}

function testAddFacts_badInputs() {
    var inputs = ["abc"];
    var db = Database();

    assertError(\-> db.addFacts(inputs),
        "Expected Nero-compatible fact, got: String 'abc'.");
}

//-------------------------------------------------------------------------
// all

// Tested in use by other tests

//-------------------------------------------------------------------------
// clear

function testClear() {
    var db = Database();
    db.addFacts([JOE]);
    db.clear();
    assertT(db.isEmpty());
    assertEQ(db.size(), 0);
    assertT(db.all().isEmpty());
    assertT(db.relations().isEmpty());
}

//-------------------------------------------------------------------------
// drop

function testDrop() {
    var db = Database([JOE, TEXAS, HAT]);
    db.drop("Thing");
    assertEQ(db.all(), {JOE_FACT, TEXAS_FACT});
    assertEQ(db.relation("Thing"), Set());
}

//-------------------------------------------------------------------------
// Equivalence

var S2N = Equivalence(#s2n,
    \s -> s ~ String() ? Number(s) : null,
    \n -> n ~ Number() ? Joe.stringify(n) : null
);

function testEquivalence() {
    var db = Database();
    db.equivalence(S2N);
    assertEQ(db.getEquivalences(), {S2N});
}

function testEquivalences_individual() {
    var db = Database();
    db.equivalences(S2N);
    assertEQ(db.getEquivalences(), {S2N});
}

function testEquivalences_individual() {
    var db = Database();
    db.equivalences(S2N);
    assertEQ(db.getEquivalences(), {S2N});
}

function testEquivalences_collection() {
    var db = Database();
    db.equivalences([S2N]);
    assertEQ(db.getEquivalences(), {S2N});
}

function testUseEquivalence() {
    var db = Database();
    db.equivalences(S2N);
    db.update(ruleset {
        define transient Str/s;
        define Both/s,n;
        Str("123");
        Both(s,n) :- Str(s), equivalent(#s2n, s, n);
    });
    var results = db.toNeroScript().strip();
    assertEQ(results, """
        define Both/s,n;
        Both("123", 123);
        """);
}


//-------------------------------------------------------------------------
// filter

function testFilter() {
    var db = Database([JOE, TEXAS, HAT]);

    var set = db.filter(\f -> f ~ Person());
    assertEQ(set, {JOE_FACT});
}

//-------------------------------------------------------------------------
// isDebug/debug

function testDebug_setGet() {
    var db = Database();
    assertF(db.isDebug());
    db.debug();
    assertT(db.isDebug());
    db.debug(false);
    assertF(db.isDebug());
}

//-------------------------------------------------------------------------
// isEmpty

// Tested in use by other tests

//-------------------------------------------------------------------------
// map

function testMap() {
    var db = Database([JOE, TEXAS, HAT]);
    var strings = db.map(Joe.stringify).sorted();
    assertEQ(strings, [
        "Fact(Person, #joe, 90)",
        "Fact(Place, #texas)",
        "Fact(Thing, #hat, #black)"
    ]);
}

//-------------------------------------------------------------------------
// query

function testQuery() {
    var db = Database([
        Parent(#anne, #bert),
        Parent(#bert, #clark)
    ]);

    var results = db.query(ruleset {
        define Ancestor/a,d;
        Ancestor(x, y) :- Parent(x, y);
        Ancestor(x, y) :- Parent(x, z), Ancestor(z, y);
    });

    var expected = {
        Fact("Ancestor", ["a", #anne, "d", #bert]),
        Fact("Ancestor", ["a", #anne, "d", #clark]),
        Fact("Ancestor", ["a", #bert, "d", #clark])
    };
    assertEQ(results, expected);
}

//-------------------------------------------------------------------------
// relation

// Tested in use by other tests

//-------------------------------------------------------------------------
// relations

// Tested in use by other tests

//-------------------------------------------------------------------------
// remove

function testRemove_good() {
    var db = Database([JOE, TEXAS, HAT]);
    db.remove(HAT);

    assertEQ(db.all(), {JOE_FACT, TEXAS_FACT});
    assertEQ(db.relation("Thing"), Set());
}

function testRemove_bad() {
    var inputs = [
        Parent(#anne, #bert),
        Parent(#bert, #clark)
    ];
    var db = Database(inputs);

    assertError(\-> db.remove("abc"),
        "Expected Nero-compatible fact, got: String 'abc'.");
}

//-------------------------------------------------------------------------
// removeAll

function testRemoveAll_collection() {
    var db = Database([JOE, TEXAS, HAT]);
    db.removeAll([HAT]);

    assertEQ(db.all(), {JOE_FACT, TEXAS_FACT});
    assertEQ(db.relation("Thing"), Set());
}

function testRemoveAll_Database() {
    var db = Database([JOE, TEXAS, HAT]);
    var other = Database([HAT]);
    db.removeAll(other);

    assertEQ(db.all(), {JOE_FACT, TEXAS_FACT});
    assertEQ(db.relation("Thing"), Set());
}

function testRemoveAll_bad() {
    var db = Database();

    assertError(\-> db.removeAll(["abc"]),
        "Expected Nero-compatible fact, got: String 'abc'.");
}

//-------------------------------------------------------------------------
// removeIf

function testRemoveIf() {
    var db = Database([JOE, TEXAS, HAT]);
    db.removeIf(\f -> f ~ Thing());
    assertEQ(db.all(), {JOE_FACT, TEXAS_FACT});
    assertEQ(db.relation("Thing"), Set());
}

//-------------------------------------------------------------------------
// rename

function testRename() {
    var db = Database([JOE, TEXAS, HAT]);
    var newFact = Fact("Location", ["id", #texas]);
    db.rename("Place", "Location");
    assertEQ(db.all(), {JOE_FACT, newFact, HAT_FACT});
    assertEQ(db.relation("Place"), Set());
    assertEQ(db.relation("Location"), {newFact});
}


//-------------------------------------------------------------------------
// size

// Tested in use by other tests

//-------------------------------------------------------------------------
// toNeroAxiom

function testToNeroAxiom() {
    // A record convertible to a fact.
    var fact = Parent(#anne, #bert);
    var db = Database();
    assertEQ(db.toNeroAxiom(fact), "Parent(#anne, #bert);");
}

//-------------------------------------------------------------------------
// toNeroScript

function testToNeroScript_noArgs() {
    var db = Database([Parent(#anne, #bert)]);
    assertEQ(db.toNeroScript().strip(), """
        define Parent/parent,child;
        Parent(#anne, #bert);
        """);
}

function testToNeroScript_args() {
    var db = Database();

    // A record convertible to a fact.
    assertEQ(db.toNeroScript([Parent(#anne, #bert)]).strip(), """
        define Parent/parent,child;
        Parent(#anne, #bert);
        """);
}

//-------------------------------------------------------------------------
// toString

// Test in use by other tests

//-------------------------------------------------------------------------
// update

function testUpdate_good() {
    var db = Database([
        Parent(#anne, #bert),
        Parent(#bert, #clark)
    ]);

    db.update(ruleset {
        define Ancestor/a,d;
        Ancestor(x, y) :- Parent(x, y);
        Ancestor(x, y) :- Parent(x, z), Ancestor(z, y);
    });

    var expected = {
        Fact("Ancestor", ["a", #anne, "d", #bert]),
        Fact("Ancestor", ["a", #anne, "d", #clark]),
        Fact("Ancestor", ["a", #bert, "d", #clark])
    };

    // The relation's set was updated.
    assertEQ(db.relation("Ancestor"), expected);

    // The set of all facts was updated.
    assertT(db.all().containsAll(expected));
}

