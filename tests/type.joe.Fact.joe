// type.joe.Fact.joe
//
// Tests for the `Fact` type

function testInit_map_good() {
    var fact = Fact("Thing", {"id": #car, "color": #red});
    assertEQ(fact.relation(), "Thing");
    assertF(fact.isOrdered());
    assertEQ(fact.fieldMap(), {"id": #car, "color": #red});
    assertEQ(fact.fieldNames().sorted(), ["color", "id"]);
    assertEQ(fact.id, #car);
    assertEQ(fact.color, #red);
}

function testInit_map_badRelation() {
    assertError(\-> Fact("@Thing", {"type": #car}),
        "Expected identifier, got: String '@Thing'.");
}

function testInit_map_badFieldName() {
    assertError(\-> Fact("Thing", {"@type": #car}),
        "Expected identifier, got: String '@type'.");
}

function testInit_list_good() {
    var fact = Fact("Thing", ["id", #car, "color", #red]);
    assertEQ(fact.relation(), "Thing");
    assertT(fact.isOrdered());
    assertEQ(fact.fields(), [#car, #red]);
    assertEQ(fact.fieldMap(), {"id": #car, "color": #red});
    assertEQ(fact.fieldNames(), ["id", "color"]);
    assertEQ(fact.id, #car);
    assertEQ(fact.color, #red);
    assertEQ(fact.toString(), "Fact(Thing, [\"id\", #car, \"color\", #red])");
}

function testInit_list_badRelation() {
    assertError(\-> Fact("@Thing", ["type", #car]),
        "Expected identifier, got: String '@Thing'.");
}

function testInit_list_badFieldName() {
    assertError(\-> Fact("Thing", ["@type", #car]),
        "Expected identifier, got: String '@type'.");
}

function testBadField() {
    var fact = Fact("Thing", ["id", #car, "color", #red]);
    assertError(\-> fact.size,
        "Undefined property 'size'.");
}

function testMatch_mapFact() {
    var fact = Fact("Thing", {"id": #car, "color": #red});
    assertF(fact ~ Thing(#car, _));
    assertT(fact ~ Thing(id: #car));
}

function testMatch_recordFact() {
    var fact = Fact("Thing", ["id", #car, "color", #red]);
    assertT(fact ~ Thing(#car, _));
    assertT(fact ~ Thing(id: #car));
    assertF(fact ~ Thing(#bus, _));
    assertF(fact ~ Thing(id: #bus));
}
