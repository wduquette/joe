// type.joe.Nero.joe

//-----------------------------------------------------------------------------
// Pipeline Execution

function testDebug() {
    var nero = Nero();
    var rules = ruleset {
        define Parent/p,c;
        Parent(#anne, #bert);
    };
    
    // Just verify that the debug flag can be set.
    nero.withRules(rules).debug().debug(false);
}

function testInfer() {
    var nero = Nero();
    var rules = ruleset {
        define Parent/p,c;
        define Ancestor/a,d;
        Parent(#anne, #bert);
        Parent(#bert, #clark);
        Ancestor(x, y) :- Parent(x, y);
        Ancestor(x, y) :- Parent(x, z), Ancestor(z, y);
    };
    var results = nero.toNeroScript(nero.withRules(rules).infer()).strip();
    assertEQ(results, """
        define Ancestor/a,d;
        Ancestor(#anne, #bert);
        Ancestor(#anne, #clark);
        Ancestor(#bert, #clark);
        
        define Parent/p,c;
        Parent(#anne, #bert);
        Parent(#bert, #clark);
        """);
}

function testWithRules_script() {
    var nero = Nero();
    var script = """
        define Parent/p,c;
        define Ancestor/a,d;
        Parent(#anne, #bert);
        Parent(#bert, #clark);
        Ancestor(x, y) :- Parent(x, y);
        Ancestor(x, y) :- Parent(x, z), Ancestor(z, y);
        """;
    var results = nero.toNeroScript(nero.withRules(script).infer()).strip();
    assertEQ(results, """
        define Ancestor/a,d;
        Ancestor(#anne, #bert);
        Ancestor(#anne, #clark);
        Ancestor(#bert, #clark);

        define Parent/p,c;
        Parent(#anne, #bert);
        Parent(#bert, #clark);
        """);
}

function testUpdate() {
    var nero = Nero();
    var rules1 = ruleset {
        define Parent/p,c;
        Parent(#anne, #bert);
        Parent(#bert, #clark);
    };
    var rules2 = ruleset {
        define Ancestor/a,d;
        Ancestor(x, y) :- Parent(x, y);
        Ancestor(x, y) :- Parent(x, z), Ancestor(z, y);
    };
    var facts = nero.withRules(rules1).infer();
    var results = nero.toNeroScript(nero.withRules(rules2).update(facts)).strip();
    assertEQ(results, """
        define Ancestor/a,d;
        Ancestor(#anne, #bert);
        Ancestor(#anne, #clark);
        Ancestor(#bert, #clark);

        define Parent/p,c;
        Parent(#anne, #bert);
        Parent(#bert, #clark);
        """);
}

function testQuery() {
    var nero = Nero();
    var rules1 = ruleset {
        define Parent/p,c;
        Parent(#anne, #bert);
        Parent(#bert, #clark);
    };
    var rules2 = ruleset {
        define Ancestor/a,d;
        Ancestor(x, y) :- Parent(x, y);
        Ancestor(x, y) :- Parent(x, z), Ancestor(z, y);
    };
    var facts = nero.withRules(rules1).infer();
    var results = nero.toNeroScript(nero.withRules(rules2).query(facts)).strip();
    assertEQ(results, """
        define Ancestor/a,d;
        Ancestor(#anne, #bert);
        Ancestor(#anne, #clark);
        Ancestor(#bert, #clark);
        """);
}

//-----------------------------------------------------------------------------
// Inputs and Automatic Fact Conversions
//
// These tests test the conversion of various non-Fact types into Facts by 
// the Nero pipeline.  They are tested here rather than in NeroTest because
// they require scripted Joe types.

// Should throw an error if an input fact has no fields.
function testWith_noFields() {
    class Parent {
        method toString() { return "Parent()"; }
    }

    var inputs = [
        Parent()
    ];

    var rules = ruleset {
        define Ancestor/a,d;
        Ancestor(x, y) :- Parent(x, y);
    };

    var nero = Nero();
    assertError(\-> nero.withRules(rules).update(inputs),
        "Expected Nero-compatible fact, got: Parent 'Parent()'.");
    assertError(\-> nero.withRules(rules).query(inputs),
        "Expected Nero-compatible fact, got: Parent 'Parent()'.");
}

// Should accept proxied values with
// fields, including the Fact type itself.
function testWith_proxiedValue_ordered() {
    var inputs = [
        Fact("Parent", ["p", #anne, "c", #bert]),
        Fact("Parent", ["p", #bert, "c", #clark])
    ];

    var rules = ruleset {
        define Ancestor/a,d;
        Ancestor(x, y) :- Parent(x, y);
        Ancestor(x, y) :- Parent(x, z), Ancestor(z, y);
    };
    var nero = Nero();
    var results = nero.toNeroScript(nero.withRules(rules).query(inputs)).strip();

    assertEQ(results, """
        define Ancestor/a,d;
        Ancestor(#anne, #bert);
        Ancestor(#anne, #clark);
        Ancestor(#bert, #clark);
        """);
}

// Should accept proxied values with
// fields, including the Fact type itself, using named atoms
function testWith_proxiedValue_named() {
    var inputs = [
        Fact("Parent", ["p", #anne, "c", #bert]),
        Fact("Parent", ["p", #bert, "c", #clark])
    ];

    var rules = ruleset {
        define Ancestor/a,d;
        Ancestor(x, y) :- Parent(p:x, c:y);
        Ancestor(x, y) :- Parent(p:x, c:z), Ancestor(z, y);
    };

    var nero = Nero();
    var results = nero.toNeroScript(nero.withRules(rules).query(inputs)).strip();

    assertEQ(results, """
        define Ancestor/a,d;
        Ancestor(#anne, #bert);
        Ancestor(#anne, #clark);
        Ancestor(#bert, #clark);
        """);
}

// Should accept record instances using
// ordered-field atoms.
function testWith_record_ordered() {
    record Parent(p, c) {}
    var inputs = [
        Parent(#anne, #bert),
        Parent(#bert, #clark)
    ];

    var rules = ruleset {
        define Ancestor/a,d;
        Ancestor(x, y) :- Parent(x, y);
        Ancestor(x, y) :- Parent(x, z), Ancestor(z, y);
    };

    var nero = Nero();
    var results = nero.toNeroScript(nero.withRules(rules).query(inputs)).strip();

    assertEQ(results, """
        define Ancestor/a,d;
        Ancestor(#anne, #bert);
        Ancestor(#anne, #clark);
        Ancestor(#bert, #clark);
        """);
}

// Should accept record instances using
// named-field atoms
function testWith_record_named() {
    record Parent(p, c) {}
    var inputs = [
        Parent(#anne, #bert),
        Parent(#bert, #clark)
    ];

    var rules = ruleset {
        define Ancestor/a,d;
        Ancestor(x, y) :- Parent(p:x, c:y);
        Ancestor(x, y) :- Parent(p:x, c:z), Ancestor(z, y);
    };

    var nero = Nero();
    var results = nero.toNeroScript(nero.withRules(rules).query(inputs)).strip();

    assertEQ(results, """
        define Ancestor/a,d;
        Ancestor(#anne, #bert);
        Ancestor(#anne, #clark);
        Ancestor(#bert, #clark);
        """);
}

// Should accept class instances with fields, provided that 
// named-field atoms are used to match them.
function testWith_class_named() {
    class Parent {
        method init(p, c) {
            @p = p;
            @c = c;
        }
        method toString() {
            return "Parent(" + @p + ", " + @c + ")";
        }
    }

    var inputs = [
        Parent(#anne, #bert),
        Parent(#bert, #clark)
    ];

    var rules = ruleset {
        define Ancestor/a,d;
        Ancestor(x, y) :- Parent(p:x, c:y);
        Ancestor(x, y) :- Parent(p:x, c:z), Ancestor(z, y);
    };

    var nero = Nero();
    var results = nero.toNeroScript(nero.withRules(rules).query(inputs)).strip();

    assertEQ(results, """
        define Ancestor/a,d;
        Ancestor(#anne, #bert);
        Ancestor(#anne, #clark);
        Ancestor(#bert, #clark);
        """);
}

// Should throw an error if an ordered-field atom is used with a 
// fact that does not have ordered fields.
function testWith_class_ordered() {
    class Parent {
        method init(p, c) {
            @p = p;
            @c = c;
        }
        method toString() {
            return "Parent(" + @p + ", " + @c + ")";
        }
    }

    var inputs = [
        Parent(#anne, #bert),
        Parent(#bert, #clark)
    ];

    var rules = ruleset {
        define Ancestor/a,d;
        Ancestor(x, y) :- Parent(x, y);
    };

    var nero = Nero();
    assertError(\-> nero.withRules(rules).query(inputs),
        "'Parent' in rule 'Ancestor(x, y) :- Parent(x, y);' requires ordered fields, but a provided fact is not ordered.");
}

function testWith_namedPattern() {
    class Thing {
        method init(id, color) {
            @id = id;
            @color = color;
        }
    }

    var inputs = [
        Fact("A", ["id", #a, "color", Thing(#hat, #black)]),
        Fact("A", ["id", #b, "color", Thing(#wagon, #red)])
    ];

    var rules = ruleset {
        define B/x,y,z;

        B(x, y, z) :- A(x, Thing(id: y, color: z));
    };

    var nero = Nero();
    var results = nero.toNeroScript(nero.withRules(rules).query(inputs)).strip();

    assertEQ(results, """
        define B/x,y,z;
        B(#a, #hat, #black);
        B(#b, #wagon, #red);
        """);
}

function testPattern_ordered() {
    record Thing(id, color) {}

    var inputs = [
        Fact("A", ["id", #a, "color", Thing(#hat, #black)]),
        Fact("A", ["id", #b, "color", Thing(#wagon, #red)])
    ];

    var rules = ruleset {
        define B/x,y,z;
        B(x, y, z) :- A(x, Thing(y, z));
    };

    var nero = Nero();
    var results = nero.toNeroScript(nero.withRules(rules).query(inputs)).strip();

    assertEQ(results, """
        define B/x,y,z;
        B(#a, #hat, #black);
        B(#b, #wagon, #red);
        """);
}

//-----------------------------------------------------------------------------
// mappers

function s2n(s) {
    return (s ~ String())? Number(s) : null;
}

function testAddMapper() {
    var nero = Nero();
    nero.addMapper(#s2n, s2n);
    var results = nero.withRules(ruleset {
        define transient Str/s;
        define Num/n;
        Str("123");
        Num(n) :- Str(s), mapsTo(#s2n, s, n);
    }).infer();

    assertEQ(nero.toNeroScript(results).strip(), """
        define Num/n;
        Num(123);
        """);
}

function testAddMappers() {
    var nero = Nero();
    nero.addMappers({#s2n: s2n});
    var results = nero.withRules(ruleset {
        define transient Str/s;
        define Num/n;
        Str("123");
        Num(n) :- Str(s), mapsTo(#s2n, s, n);
    }).infer();

    assertEQ(nero.toNeroScript(results).strip(), """
        define Num/n;
        Num(123);
        """);
}

//-----------------------------------------------------------------------------
// toNero*

// toNeroScript is tested in-line above.

function testToNeroAxiom() {
    var nero = Nero();
    var rules = ruleset {
        define Parent/p,c;
        Parent(#anne, #bert);
        Parent(#bert, #clark);
    };
    var facts = nero.withRules(rules).infer();
    var list = facts.map(nero.toNeroAxiom).sorted();
    assertEQ(list, [
        "Parent(#anne, #bert);",
        "Parent(#bert, #clark);",
    ]);
}
