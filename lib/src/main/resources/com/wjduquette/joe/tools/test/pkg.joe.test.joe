// `joe test`'s Test Library

//**
// @package joe.test

//**
// @function check
// @args value
// @result ValueChecker
// Returns a checker for testing a computed value.
function check(value) {
    return ValueChecker(value);
}

//**
// @function checkCatch
// @args callable
// @result CatchChecker
// Executes a no-arg *callable* and returns a checker for checking
// the error result.
function checkCatch(callable) {
    return CatchChecker(catch(callable));
}

//**
// @type ValueChecker
// A fluent test checker for arbitrary values.
// Use `check(value)` to create one.
class ValueChecker {
    //**
    // @init
    // @args value
    // Initializes the new checker with the given *value*.
    method init(value) {
        @value = value;
    }

    //**
    // @method eq
    // @args expected
    // @result this
    // Checks that the value is equal to the *expected* value.
    method eq(expected) {
        assert @value == expected, @_expected(_typedValue(expected));
        return this;
    }

    //**
    // @method isTrue
    // @result this
    // Checks that the value is truthy.
    method isTrue() {
        assert @value, @_expected("true");
        return this;
    }

    //**
    // @method isFalse
    // @result this
    // Checks that the value is falsey.
    method isFalse() {
        assert !@value, @_expected("false");
        return this;
    }

    //**
    // @method isNull
    // @result this
    // Checks that the value is `null`
    method isNull() {
        assert @value == null, @_expected("null");
        return this;
    }

    //**
    // @method isNotNull
    // @result this
    // Checks that the value is not `null`
    method isNonNull() {
        assert @value != null, @_expected("non-null");
        return this;
    }

    //**
    // @method isEmpty
    // @result this
    // Checks that the value `isEmpty()` according to its own `isEmpty()`
    // method.
    method isEmpty() {
        assert @value.isEmpty(), @_expected("isEmpty()");
    }

    //**
    // @method containsAll
    // @args values...
    // @result this
    // Assumes that the value is a collection, and verifies that it
    // contains all of the expected arguments.
    method containsAll(args) {
        // TODO: Need predicate to check for a collection
        foreach (var arg : args) {
            if (!@value.contains(arg)) {
                fail("Expected collection to contain " +
                    _typedValue(arg) + ", got: " + _typedValue(args) + ".");
            }
        }
    }

    //**
    // @method hasType
    // @args type
    // @result this
    // Checks that the value has the given type.
    method hasType(type) {
        var got = Joe.typeOf(@value);
        assert got == type,
            @_expected2("type '" + type + "'", got);
    }

    //**
    // @method hasTypeName
    // @args name
    // @result this
    // Checks that the value's type has the given name.
    method hasTypeName(name) {
        var got = Joe.typeOf(@value).name();
        assert got == name,
            @_expected2("type name '" + name + "'", got);
    }

    method _expected(what) {
        return "Expected " + what + ", got: " +
            _typedValue(@value) + ".";
    }
    method _expected2(what, got) {
        return "Expected " + what + ", got: " +
            _typedValue(got) + ".";
    }
}

//**
// @type CatchChecker
// A fluent test checker for `catch()` results.
// Use `checkCatch(callable)` to create it.
class CatchChecker {
    //**
    // @init
    // @args tuple
    // Initializes the checker with the result of a `catch()`.
    method init(tuple) {
        @tuple = tuple;
        @err = tuple.is(#error) ? tuple.error : null;
    }

    //**
    // @method isOK
    // @result this
    // Checks that the catch result is not an error.
    method isOK() {
        assert @err == null,
            "Expected no error, got: '"
            + @err.message() + "'.";
        return this;
    }

    //**
    // @method isError
    // @result Boolean
    // Returns `true` if there was an error, and `false` otherwise.
    method isError() {
        assert @err != null, "Expected an error, got: "
            + _typedValue(@tuple.ok) + ".";
        return this;
    }

    //**
    // @method message
    // @args expected
    // @result this
    // Checks that the error message matches the *expected* value.
    method message(expected) {
        @isError();
        assert @err.message() == expected,
            "Expected message '" + expected + "', got: '" +
            @err.message() + "'.";
        return this;
    }

    //**
    // @method type
    // @args expected
    // @result this
    // Checks the error type matches the *expected* value.
    method type(expected) {
        @isError();
        assert @err.type() == expected,
            "Expected error type '" + expected + "', got: '" + @err.type() + "'.";
        return this;
    }

    //**
    // @method stackFrames
    // @args frame,...
    // @result this
    // Checks that the actual stack frames match the expected list of
    // stack frames. The expected frames are passed as individual arguments.
    method stackFrames(args) {
        @isError();
        assert @err.stackFrames() == args,
            "Expected stack frames '" + args + "', got: '" + @err.stackFrames() + "'.";
        return this;
    }

    //**
    // @method stackTrace
    // @args expected
    // @result this
    // Checks that the actual stack traces matches the expected stack trace
    // string.
    method stackTrace(expected) {
        @isError();
        assert @err.stackTrace() == expected,
            "Expected stack trace '" + expected + "', got: '" + @err.stackTrace() + "'.";
        return this;
    }
}

function _typedValue(value) {
    if (value == null) {
        return "'null'";
    } else {
        return Joe.typeOf(value).name() + " '" + value + "'";
    }
}
